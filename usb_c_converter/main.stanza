; Generated by JITX 2.28.0
#use-added-syntax(jitx)
defpackage main :
  import core
  import jitx
  import jitx/commands
  import ocdb/utils/bundles
  import ocdb/utils/defaults
  import ocdb/utils/design-vars
  import helpers
  import si-tools
  import custom_stackups

; Define the shape/size of the board
val board-shape = RoundedRectangle(45.67, 25.4, 1.61)

OPERATING-TEMPERATURE = min-max(-10.0, 50.0)

pcb-module power-regulation :
  port vbus : power
  port power-3v3 : power

  inst buck : components/Texas-Instruments/TPS6208x/module(vout-V = 3.3)

  net (vbus, buck.vin)
  net (power-3v3, buck.vout)

  net GND (vbus.gnd)
  net pwr5v0 (vbus.vdd)
  net pwr3v3 (power-3v3.vdd)

  symbol(GND) = ocdb/utils/symbols/ground-sym
  symbol(pwr5v0) = ocdb/utils/symbols/supply-sym
  symbol(pwr3v3) = ocdb/utils/symbols/supply-sym

doc: \<DOC>
ESD Protection Generator

This module instantiates some number of ESD protection devices and 
then combines the supports from all of these devices in one module.
This allows the higher level code to construct a PA problem where
differential pairs and single-ended signals can use the appropriate 
ESD protection channel without requiring any pin swapping in code.

It "Just Works" (TM)
<DOC>
pcb-module esd-protection :
  pin GND

  inst IC : components/Texas-Instruments/TPDxE05U06/component(channels = 6)[2]

  for i in 0 to length(IC) do:
    net (IC[i].GND, GND)
    for j in 0 to 3 do:
      supports bidir-diff-pair:
        require ch:bidir-diff-pair from IC[i]
        bidir-diff-pair.A.P => ch.A.P
        bidir-diff-pair.A.N => ch.A.N
        bidir-diff-pair.B.P => ch.B.P
        bidir-diff-pair.B.N => ch.B.N

    for j in 0 to 6 do:
      supports pass-through:
        require pt:pass-through from IC[i]
        pass-through.A => pt.A
        pass-through.B => pt.B

; Module to run as a design
pcb-module my-design :
  ; Connectors 
  inst usb-a : components/XKB-Connectivity/U231-096N-3BLRT06-SS/module
  inst usb-c : components/Molex/_2012670005/module

  ; Power
  inst power : power-regulation
  net (usb-a.usb.vbus usb-c.usb.vbus power.vbus)
  
  ; USB SuperSpeed Mux 
  inst usb3-mux : components/Texas-Instruments/HD3SS3220RNHR/module
  net (usb3-mux.power-3v3, power.power-3v3)

  net (usb3-mux.usb-c.vbus, usb-c.usb.vbus)
  net (usb3-mux.usb-c.cc, usb-c.usb.cc)

  ; Connect the D+, D- diffpairs across the USBA and C connectors and USB2 Controller
  inst usb2-driver : components/Texas-Instruments/TUSB211IRWBR/module
  net (usb2-driver.power, power.power-3v3)

  inst util-esd : components/Texas-Instruments/TPDxE05U06/component(channels = 4)

  require repeater : bidir-diff-pair from usb2-driver
  require esd-data : bidir-diff-pair from util-esd

  topo-net(
    usb-a.usb.data => 
    esd-data.A => 
    esd-data.B => 
    repeater.A => 
    repeater.B => 
    usb-c.usb.data[1] => 
    usb-c.usb.data[2]
  )

  ; The total intra-pair skew allowed on the USB2.0 Data Pair
  val USB2-skew = 7.5e-12 / 2.0
  skew-window(0.0 +/- USB2-skew, usb-a.usb.data => usb-c.usb.data[2])
  max-loss(12.0, usb-a.usb.data => usb-c.usb.data[2])
  diff-structure(diff-90, usb-a.usb.data => usb-c.usb.data[2])

  inst ss-esd : esd-protection
  net GND (power.vbus.gnd, ss-esd.GND, util-esd.GND)

  require esd-1 : bidir-diff-pair[2] from ss-esd
  topo-net(usb3-mux.usb-c.sstx[1] => esd-1[0].A => esd-1[0].B => usb-c.usb.sstx[1])
  topo-net(usb3-mux.usb-c.ssrx[1] => esd-1[1].A => esd-1[1].B => usb-c.usb.ssrx[1])

  require esd-2 : bidir-diff-pair[2] from ss-esd
  topo-net(usb3-mux.usb-c.sstx[2] => esd-2[0].A => esd-2[0].B => usb-c.usb.sstx[2])
  topo-net(usb3-mux.usb-c.ssrx[2] => esd-2[1].A => esd-2[1].B => usb-c.usb.ssrx[2])

  val USB3-skew = 1.0e-12
  for i in [1, 2] do:
    val tx-src = get-signal-end(usb3-mux.usb-c.sstx[i])
    val rx-dst = get-signal-end(usb3-mux.usb-c.ssrx[i])
    diff-structure(
      diff-90, 
      tx-src => usb-c.usb.sstx[i],
      rx-dst => usb-c.usb.ssrx[i]
    )

    skew-window(
      0.0 +/- USB3-skew, 
      tx-src => usb-c.usb.sstx[i],
      rx-dst => usb-c.usb.ssrx[i]
    )

    max-loss(
      12.0,
      tx-src => usb-c.usb.sstx[i]
      rx-dst => usb-c.usb.ssrx[i]
      )

  ; Type A to Mux 
  
  ; NOTE: Because we are using a type-A connector which 
  ;  is typically the host - we must swap the TX / RX 
  ;  pairs.
  ;  This is why the TX on Mux => RX on Connector
  ;    and RX on Mux => TX on Connector
  val conn-tx-ep = usb-a.usb.ssrx
  val conn-rx-ep = usb-a.usb.sstx

  topo-net(usb3-mux.usb-a.ssrx => conn-rx-ep)
  topo-net(usb3-mux.usb-a.sstx => conn-tx-ep)

  val mux-rx-ep = property(usb3-mux.usb-a.ssrx.signal-end)
  val mux-tx-ep = property(usb3-mux.usb-a.sstx.signal-end)


  diff-structure(
    diff-90, 
    mux-tx-ep => conn-tx-ep,
    mux-rx-ep => conn-rx-ep,
    )

  skew-window(
    0.0 +/- USB3-skew, 
    mux-tx-ep => conn-tx-ep,
    mux-rx-ep => conn-rx-ep,
  )

  max-loss(
    12.0,
    mux-tx-ep => conn-tx-ep,
    mux-rx-ep => conn-rx-ep,
    )


  require pt : pass-through[4] from ss-esd
  net (pt[0].A, usb-c.usb.sbu[1])
  net (pt[1].A, usb-c.usb.cc[1])
  net (pt[2].A, usb-c.usb.sbu[2])
  net (pt[3].A, usb-c.usb.cc[2])

  geom(GND) :
    copper-pour(LayerIndex(1), isolate = 0.125, rank = 1) = board-shape
    copper-pour(LayerIndex(4), isolate = 0.125, rank = 1) = board-shape
    
; Set the design name - a directory with this name will be generated under the "designs" directory
set-current-design("USB-A-to-C")


public pcb-board my-board (outline:Shape) :
  stackup = sierra-circuits-6-layer-100z-dif
  boundary = outline
  signal-boundary = outline
  vias = [default-via minimum-via]


; Setup the board
defn setup-board () :
  set-board(my-board(board-shape))
  set-rules(custom_rules/sierra-circuits-notouch-rules-100z-diff) ;manufacturing rules

; Set the schematic sheet size
set-paper(ANSI-B)

; Setup the board properties
setup-board()

; Set the top level module (the module to be compile into a schematic and PCB)
set-main-module(my-design)

; Use any helper function from helpers.stanza here
; run-check-on-design(my-design)

; View the results
view-board()
view-schematic()
view-design-explorer()