; Generated by JITX 2.28.0
#use-added-syntax(jitx)
defpackage main :
  import core
  import jitx
  import jitx/commands
  import ocdb/utils/generic-components
  import ocdb/utils/symbols
  import ocdb/utils/bundles
  import helpers
  import custom_stackups


; Define the shape/size of the board
val board-shape = RoundedRectangle(45.67, 25.4, 1.61)

pcb-module power-regulation :
  port vbus : power
  port power-3v3 : power

  inst buck : components/Texas-Instruments/TPS62082DSGT/module

  net (vbus buck.power-5v0)
  net GND (vbus.gnd power-3v3.gnd)
  net pwr5v0 (vbus.vdd)
  net pwr3v3 (buck.power-3v3.vdd)

  symbol(GND) = ocdb/utils/symbols/ground-sym
  symbol(pwr5v0) = ocdb/utils/symbols/supply-sym
  symbol(pwr3v3) = ocdb/utils/symbols/supply-sym

; Module to run as a design
pcb-module my-design :

  inst usb-a : components/Amphenol/GSB3211311WEU/module
  inst usb-c : components/Molex/_2012670005/module
  inst power : power-regulation
  net (usb-a.usb.vbus usb-c.usb.vbus power.vbus)
  
  inst usb3-mux : components/Texas-Instruments/HD3SS3220RNHR/module
  net (usb3-mux.power-3v3, power.power-3v3)
  net (usb3-mux.usb-a usb-a.usb)
  net (usb3-mux.usb-c usb-c.usb)

  ; Connect the D+, D- diffpairs across the USBA and C connectors and USB2 Controller
  inst usb2-driver : components/Texas-Instruments/TUSB211IRWBR/module
  net (usb2-driver.data usb-a.usb.data usb-c.usb.data[1] usb-c.usb.data[2])
  net (usb2-driver.power, power.power-3v3)

  ; ESD Protection
  inst ss-esd : components/Texas-Instruments/TPDxE05U06/component(channels = 4)[2]
  inst util-esd : components/Texas-Instruments/TPDxE05U06/component(channels = 6)

  net GND (power.vbus.gnd ss-esd[0].GND ss-esd[1].GND util-esd.GND)

  require esd-1 : diff-pair[2] from ss-esd[0]
  net (esd-1[0] usb-c.usb.sstx[1])
  net (esd-1[1] usb-c.usb.ssrx[1])

  require esd-2 : diff-pair[2] from ss-esd[1]
  net (esd-2[0] usb-c.usb.sstx[2])
  net (esd-2[1] usb-c.usb.ssrx[2])

  require esd-3 : diff-pair[3] from util-esd
  net (esd-3[0].N usb-a.usb.data.N)
  net (esd-3[0].P usb-a.usb.data.P)
  net (esd-3[1].N usb-c.usb.sbu[1])
  net (esd-3[1].P usb-c.usb.sbu[2])
  net (esd-3[2].N usb-c.usb.cc[1])
  net (esd-3[2].P usb-c.usb.cc[2])

  ;assign a net to a layer
  geom(GND) :
    copper-pour(LayerIndex(1), isolate = 0.125, rank = 1) = board-shape
    copper-pour(LayerIndex(4), isolate = 0.125, rank = 1) = board-shape
    
; Set the design name - a directory with this name will be generated under the "designs" directory
set-current-design("USB-A-to-C")

; Setup the board
defn setup-board () :
  set-board(ocdb/utils/defaults/default-board(sierra-circuits-6-layer-100z-dif, board-shape))
  set-rules(custom_rules/sierra-circuits-notouch-rules-100z-diff) ;manufacturing rules

; Set the schematic sheet size
set-paper(ANSI-B)

; Setup the board properties
setup-board()

; Set the top level module (the module to be compile into a schematic and PCB)
set-main-module(my-design)

; Use any helper function from helpers.stanza here
; run-check-on-design(my-design)

; View the results
view-board()
view-schematic()
view-design-explorer()