; This file is generated based on the parts database query below:")
;   database-part(["manufacturer" => "Texas Instruments", "mpn" => "TPS62082DSGT"])
#use-added-syntax(jitx)
defpackage components/Texas-Instruments/TPS6208x :
  import core
  import jitx
  import jitx/commands
  import ocdb/utils/bundles
  import ocdb/utils/generic-components
  import ocdb/utils/box-symbol
  import ocdb/utils/generator-utils
  import ocdb/modules/passive-circuits

pcb-pad rectangle-smd-pad :
  name = "rectangle-smd-pad"
  type = SMD
  shape = Rectangle(0.521, 0.250)
  layer(SolderMask(Top)) = Rectangle(0.621, 0.350)
  layer(Paste(Top)) = Rectangle(0.521, 0.250)

pcb-pad rectangle-smd-pad-1 :
  name = "rectangle-smd-pad-1"
  type = SMD
  shape = Rectangle(0.900, 1.600)
  layer(SolderMask(Top)) = Rectangle(1.000, 1.700)
  layer(Paste(Top)) = Rectangle(0.900, 1.600)

pcb-landpattern lp :
  pad p[1] : rectangle-smd-pad at loc(-0.955, 0.749) on Top
  pad p[2] : rectangle-smd-pad at loc(-0.955, 0.249) on Top
  pad p[3] : rectangle-smd-pad at loc(-0.955, -0.251) on Top
  pad p[4] : rectangle-smd-pad at loc(-0.955, -0.751) on Top
  pad p[5] : rectangle-smd-pad at loc(0.946, -0.751) on Top
  pad p[6] : rectangle-smd-pad at loc(0.946, -0.251) on Top
  pad p[7] : rectangle-smd-pad at loc(0.946, 0.249) on Top
  pad p[8] : rectangle-smd-pad at loc(0.946, 0.749) on Top
  pad p[9] : rectangle-smd-pad-1 at loc(-0.005, -0.001) on Top

  layer(Silkscreen("F-SilkS", Top)) = Text(">REF", 1.0, W, loc(-1.500, 2.767), "", TrueTypeFont, false, false)
  layer(Finish(Top)) = Text(">VALUE", 1.0, W, loc(-1.500, 1.767), "", TrueTypeFont, false, false)
  layer(Silkscreen("F-SilkS", Top)) = Line(0.120, [Point(-1.044, -1.062), Point(1.054, -1.062)])
  layer(Silkscreen("F-SilkS", Top)) = Line(0.120, [Point(1.045, 1.062), Point(-1.054, 1.062)])
  layer(Finish(Top)) = Polyline(0.060, [
    Arc(-1.005, 0.999, 0.030, 0.0, -360.000)])
  layer(Finish(Top)) = Polyline(0.200, [
    Arc(-1.385, 0.759, 0.100, 0.0, -360.000)])
  layer(Silkscreen("F-SilkS", Top)) = Polyline(0.254, [
    Arc(-1.402, 1.142, 0.076, 0.0, -360.000)])
  layer(Courtyard(Top)) = Rectangle(2.109, 2.123)

doc: \<DOC>
Family of low-voltage DC/DC with 1.2A Max current
<DOC>
public pcb-component component (-- vout-V:Double):
  name = "Buck Converter"

  if vout-V == 3.3:
    description = "Step-down Buck Converter, 3.3V Fixed Output"
    mpn = "TPS62082DSGT"
    property(self.fixed-output) = 3.3
    property(self.adjustable) = false
  else if vout-V == 1.8:
    description = "Step-down Buck Converter, 1.8V Fixed Output"
    mpn = "TPS62081DSGT"
    property(self.fixed-output) = 1.8
    property(self.adjustable) = false
  else:
    description = "Step-down Buck Converter, Adjustable Output"
    mpn = "TPS62080DSGT"
    property(self.adjustable) = true
  
  manufacturer = "Texas Instruments"
  datasheet = "https://www.ti.com/lit/ds/symlink/tps62082.pdf"
  reference-prefix = "U"

  property(self.rated-temperature) = min-max(-40.0, 85.0)

  pin-properties :
    [pin:Ref | pads:Ref ... | side:Dir | electrical-type:String | bank:Int]

    [VIN | p[8] | Left | "unspecified" | 0]
    [EN | p[1] | Left | "unspecified" | 0]
    [MODE | p[3] | Left | "unspecified" | 0]
    [GND | p[2] | Left | "unspecified" | 0]
    [EP | p[9] | Left | "unspecified" | 0]

    [PG | p[6] | Right | "unspecified" | 0]
    [SW | p[7] | Right | "unspecified" | 0]
    [VOS | p[5] | Right | "unspecified" | 0]
    [FB | p[4] | Right | "unspecified" | 0]

  assign-landpattern(lp)
  make-box-symbol()

  property(self.VIN.op-range) = min-max(2.3, 6.0)

public pcb-module module (-- vout-V:Double) :
  port vin : power
  port vout : power

  inst buck : component(vout-V = vout-V)

  net (vin.vdd buck.VIN buck.EN)
  net (vin.gnd vout.gnd buck.GND buck.MODE buck.EP)

  val V-safety-factor = 2.0
  val vin-min-rating = max-value(property(buck.VIN.op-range)) * V-safety-factor

  bypass-cap-strap(
    buck.VIN, buck.GND,
    ["capacitance" => 10.0e-6, "min-rated-voltage" => vin-min-rating]
  )
  res-strap(buck.PG, vout.vdd, 178.0e3)

  inst l : database-part(["mpn" => "NR3015T1R0N", "manufacturer" => "Taiyo Yuden"])
  net (buck.SW, l.p[1])

  net VOUT (l.p[2], vout.vdd, buck.VOS)

  property(self.vout.vdd.voltage) = vout-V

  val vout-min-rating = V-safety-factor * vout-V
  bypass-cap-strap(
    l.p[2], buck.GND,
    ["capacitance" => 22.0e-6, "min-rated-voltage" => vout-min-rating]
  )

  ; Create the feedback divider for the power converter here.
  ; Note that we place regardless and then DNP if not adjustable
  inst vdiv : voltage-divider(
    source-voltage = typ(vout-V),
    divider-output = vout-targ,
    current = 0.0005
    ) where:
    val V-ref = min-typ-max(0.438 0.45 0.462)  ; Datasheet, Section 7.5, VFB
    val vout-targ = typ(V-ref) +/- (3 %)

  net (VOUT, vdiv.in)
  net (buck.FB, vdiv.out)
  net (buck.GND, vdiv.lo)

  if not property(buck.adjustable):
    do-not-populate(vdiv.r-hi)
    do-not-populate(vdiv.r-lo)
