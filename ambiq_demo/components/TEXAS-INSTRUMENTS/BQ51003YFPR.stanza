#use-added-syntax(jitx)
defpackage ambiq_demo/components/TEXAS-INSTRUMENTS/BQ51003YFPR :
  import core
  import collections
  import math
  import jitx
  import jitx/commands

  import ocdb/utils/landpatterns
  import ocdb/utils/symbols
  import ocdb/utils/box-symbol
  import ocdb/utils/bundles
  import ocdb/utils/generator-utils
  import ocdb/utils/generic-components

  import ambiq_demo/bundles

pcb-landpattern lp :
  make-bga-landpattern(7, 4, 0.400, 0.250, typ(1.6), typ(2.8))
  model3d = Model3D("3d-models/BQ51003YFPR--3DModel-STEP-56544.STEP",
    Vec3D(0.0, 0.0, 0.0),
    Vec3D(1.0, 1.0, 1.0),
    Vec3D(-90.0, 0.0, 0.0))

public pcb-component component :
  name = "BQ51003YFPR"
  mpn = "BQ51003YFPR"
  manufacturer = "Texas Instruments"
  description = "IC WIRELESS PWR RCVR 28DSBGA"
  datasheet = "https://www.ti.com/lit/gpn/bq51003"
  pin-properties :
    [ pin : Ref | pads : Ref ...       | side:Dir ]    
    [  AC1      |  B[3] B[4]           | Right ]
    [  AC2      |  B[1] B[2]           | Right ]
    [  AD       |  G[4]                | Left  ]
    [  nAD-EN   |  F[3]                | Right ]
    [  BOOT1    |  C[4]                | Up    ]
    [  BOOT2    |  C[1]                | Up    ]
    [  nCHG     |  F[4]                | Right ]
    [  CLAMP1   |  E[3]                | Right ]
    [  CLAMP2   |  E[2]                | Right ]
    [  COMM1    |  E[4]                | Right ]
    [  COMM2    |  E[1]                | Right ]
    [  EN1      |  G[3]                | Left  ]
    [  EN2      |  G[2]                | Left  ]
    [  FOD      |  F[2]                | Left  ]
    [  ILIM     |  G[1]                | Left  ]
    [  OUT      |  D[1] D[2] D[3] D[4] | Right ]
    [  PGND     |  A[1] A[2] A[3] A[4] | Down  ]
    [  RECT     |  C[2] C[3]           | Down  ]
    [  TS-CTRL  |  F[1]                | Left  ]

  make-box-symbol()
  assign-landpattern(lp)

public pcb-module module :
  pin VBUS
  pin GND
  port CMD : wpwr-ctl

  public inst wpwr : ambiq_demo/components/TEXAS-INSTRUMENTS/BQ51003YFPR/component

 ; speakers
  inst coil-pin : smd-testpoint-cmp(1.0)[2]
  component-status(coil-pin[0]) :
    bom-status = NotInBOM
  component-status(coil-pin[1]) :
    bom-status = NotInBOM

  inst ren1 : chip-resistor(0.0)
  inst ren2 : chip-resistor(0.0)
  component-status(ren1) :
    bom-status = NotInBOM
  component-status(ren2) :
    bom-status = NotInBOM

  net (ren1.p[1] wpwr.EN1)
  net (ren1.p[2] CMD.EN1)
  net (ren2.p[1] wpwr.EN2)
  net (ren2.p[2] CMD.EN2)

  net (CMD.nCHG wpwr.nCHG   )
  net (CMD.CTRL wpwr.TS-CTRL)
  net (VBUS     wpwr.OUT    )
  net (GND      wpwr.PGND   )

  bypass-cap-strap(wpwr.OUT GND 2.2e-6)

  res-strap(wpwr.ILIM wpwr.FOD 845.0)
  res-strap(wpwr.FOD GND 196.0)
  res-strap(wpwr.FOD wpwr.RECT 20.0e3)

  res-strap(wpwr.EN1 GND 10.0e3)
  res-strap(wpwr.EN2 GND 10.0e3)
  res-strap(wpwr.TS-CTRL GND 10.0e3)
  res-strap(wpwr.AD GND 1.0e3)

  bypass-cap-strap(wpwr.CLAMP1 wpwr.AC1 470.0e-9)
  bypass-cap-strap(wpwr.CLAMP2 wpwr.AC2 470.0e-9)

  bypass-cap-strap(wpwr.COMM1 wpwr.AC1 22.0e-9)
  bypass-cap-strap(wpwr.COMM2 wpwr.AC2 22.0e-9)

  bypass-cap-strap(wpwr.BOOT1 wpwr.AC1 10.0e-9)
  bypass-cap-strap(wpwr.BOOT2 wpwr.AC2 10.0e-9)

  bypass-cap-strap(wpwr.AC1, wpwr.AC2, 270.0e-12)

  bypass-cap-strap(wpwr.AC1 coil-pin[0].p 10.0e-9)
  net (coil-pin[1].p wpwr.AC2)

  bypass-cap-strap(wpwr.RECT, GND, 10.0e-6)
  bypass-cap-strap(wpwr.RECT, GND, 4.7e-6)

  bypass-cap-strap(VBUS, GND, 4.7e-6)

  layout-group(self) = wireless-power
  schematic-group(self) = wireless-power
