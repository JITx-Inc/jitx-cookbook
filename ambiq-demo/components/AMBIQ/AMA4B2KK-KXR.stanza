#use-added-syntax(jitx)
defpackage ambiq-demo/components/AMBIQ/AMA4B2KK-KXR :
  import core
  import collections
  import jitx
  import jitx/commands
  import ocdb/utils/box-symbol
  import ocdb/utils/defaults
  import ocdb/utils/landpatterns
  import ocdb/utils/symbols
  import ocdb/utils/symbol-utils
  import ocdb/utils/generic-components
  import ocdb/utils/bundles
  import ocdb/utils/property-structs
  import ambiq-demo/components/AMBIQ/AMA4B2KK-KXR-support

public pcb-component component :
  description = "AMA4B2KK-KXR-B2"
  pin-properties :
    [pin:Ref        | pads:Ref ...  | side:Dir  | bank:Ref]
    [GPIO[0]          | H3            | Left      | A       ]
    [GPIO[1]          | J4            | Left      | A       ]
    [GPIO[2]          | K4            | Left      | A       ]
    [GPIO[3]          | L4            | Left      | A       ]
    [GPIO[5]          | H4            | Left      | A       ]
    [GPIO[6]          | J5            | Left      | A       ]
    [GPIO[7]          | J6            | Left      | A       ]
    [GPIO[8]          | E8            | Left      | A       ]
    [GPIO[9]          | E7            | Left      | A       ]
    [GPIO[10]         | E6            | Left      | A       ]
    [GPIO[11]         | E4            | Left      | A       ]
    [GPIO[12]         | B4            | Left      | A       ]
    [GPIO[13]         | D5            | Left      | A       ]
    [GPIO[14]         | D6            | Left      | A       ]
    [GPIO[15]         | E5            | Left      | A       ]
    [GPIO[16]         | D7            | Left      | A       ]
    [GPIO[17]         | D4            | Left      | A       ]
    [GPIO[18]         | D3            | Left      | A       ]
    [GPIO[19]         | C3            | Left      | A       ]
    [GPIO[20]         | K3            | Left      | A       ]
    [GPIO[21]         | J3            | Left      | A       ]
    [GPIO[22]         | F3            | Left      | A       ]
    [GPIO[23]         | F2            | Left      | A       ]
    [GPIO[24]         | E2            | Left      | A       ]
    [GPIO[25]         | K5            | Left      | A       ]
    [GPIO[26]         | L5            | Left      | A       ]
    [GPIO[27]         | M5            | Left      | A       ]
    [GPIO[28]         | E3            | Left      | A       ]
    [GPIO[29]         | F4            | Left      | A       ]
    [GPIO[30]         | G4            | Left      | A       ]
    [GPIO[31]         | H2            | Left      | A       ]
    [GPIO[32]         | G2            | Left      | A       ]
    [GPIO[33]         | G3            | Left      | A       ]
    [GPIO[37]         | F11           | Left      | A       ]
    [GPIO[38]         | F12           | Left      | A       ]
    [GPIO[39]         | C9            | Left      | A       ]
    [GPIO[40]         | C10           | Left      | A       ]
    [GPIO[41]         | E9            | Left      | A       ]
    [GPIO[42]         | D9            | Left      | A       ]
    [GPIO[43]         | D10           | Right     | A       ]
    [GPIO[44]         | E10           | Right     | A       ]
    [GPIO[45]         | F10           | Right     | A       ]
    [GPIO[47]         | F1            | Right     | A       ]
    [GPIO[48]         | E1            | Right     | A       ]
    [GPIO[49]         | D1            | Right     | A       ]
    [GPIO[50]         | A9            | Right     | A       ]
    [GPIO[51]         | B9            | Right     | A       ]
    [GPIO[56]         | J7            | Right     | A       ]
    [GPIO[57]         | J8            | Right     | A       ]
    [GPIO[61]         | K6            | Right     | A       ]
    [GPIO[62]         | L6            | Right     | A       ]
    [GPIO[63]         | M6            | Right     | A       ]
    [GPIO[64]         | B6            | Right     | A       ]
    [GPIO[65]         | A7            | Right     | A       ]
    [GPIO[66]         | A8            | Right     | A       ]
    [GPIO[67]         | C6            | Right     | A       ]
    [GPIO[68]         | B7            | Right     | A       ]
    [GPIO[69]         | B8            | Right     | A       ]
    [GPIO[70]         | C5            | Right     | A       ]
    [GPIO[71]         | C7            | Right     | A       ]
    [GPIO[72]         | C8            | Right     | A       ]
    [GPIO[73]         | D8            | Right     | A       ]
    [GPIO[74]         | G11           | Right     | A       ]
    [GPIO[75]         | H11           | Right     | A       ]
    [GPIO[76]         | J11           | Right     | A       ]
    [GPIO[77]         | K11           | Right     | A       ]
    [GPIO[78]         | L11           | Right     | A       ]
    [GPIO[79]         | M11           | Right     | A       ]
    [GPIO[80]         | K10           | Right     | A       ]
    [GPIO[81]         | L10           | Right     | A       ]
    [GPIO[82]         | L9            | Right     | A       ]
    [GPIO[83]         | K9            | Right     | A       ]
    [GPIO[84]         | J9            | Right     | A       ]
    [GPIO[85]         | H9            | Right     | A       ]
    [GPIO[86]         | G10           | Right     | A       ]
    [GPIO[87]         | G9            | Right     | A       ]
    [GPIO[88]         | F9            | Right     | A       ]
    [GPIO[91]         | K2            | Right     | A       ]
    [GPIO[93]         | M4            | Right     | A       ]
    [GPIO[104]        | J2            | Right     | A       ]
    [RSTN           | A6            | Left      | A       ]
    [USB0PN         | M8            | Left      | B       ]
    [USB0PP         | M7            | Left      | B       ]
    [VDDP           | B1            | Up        | B       ]
    [VDDH           | M10           | Up        | B       ]
    [VDDA           | C4            | Up        | B       ]
    [LPADC_VREF     | A12           | Right     | B       ]
    [SIMOBUCK_SW    | A1            | Up        | B       ]
    [SIMOBUCK_SWSEL | A2            | Up        | B       ]
    [VDDC           | A3            | Up        | B       ]
    [VDDF           | C2            | Up        | B       ]
    [VDDS           | C1            | Up        | B       ]
    [VDDB           | G1            | Up        | B       ]
    [VDDBH_SW       | H12           | Up        | B       ]
    [VDDBH          | G12           | Up        | B       ]
    [VDDH2          | M9            | Up        | B       ]
    [VDD18          | H10           | Up        | B       ]
    [VDDUSB0P9      | L7            | Up        | B       ]
    [VDDUSB33       | K7            | Up        | B       ]
    [VDDAUDA        | B11           | Up        | B       ]
    [VDDAUDD        | E11           | Up        | B       ]
    [VDDBH_RF       | J1            | Up        | B       ]
    [VDDC_LV        | B3            | Up        | B       ]
    [LPMICBIAS      | B10           | Right     | B       ]
    [MIPI_D0N       | L12           | Left      | B       ]
    [MIPI_CLKN      | J12           | Left      | B       ]
    [MIPI_CLKP      | K12           | Left      | B       ]
    [VSSVCO         | L1            | Down      | B       ]
    [VSSS           | L2            | Down      | B       ]
    [VSSB           | H1            | Down      | B       ]
    [VSSP           | B2            | Down      | B       ]
    [VSSA           | B5            | Down      | B       ]
    [VSS            | D2            | Down      | B       ]
    [VSSAUSB        | L8            | Down      | B       ]
    [VSSAUDD        | E12           | Down      | B       ]
    [VSSAUDA        | B12           | Down      | B       ]
    [VSS18          | J10           | Down      | B       ]
    [LPADCD0PSE1    | A11           | Left      | B       ]
    [LPADCD0NSE0    | A10           | Left      | B       ]
    [MIPI_D0P       | M12           | Left      | B       ]
    [LPADCD1PSE3    | D11           | Left      | B       ]
    [RFIOP          | M2            | Left      | B       ]
    [RFIOM          | M1            | Left      | B       ]
    [TXEN           | K1            | Left      | B       ]
    [XO32M          | D12           | Left      | B       ]
    [XI32M          | C12           | Left      | B       ]
    [XO             | A5            | Right     | B       ]
    [XI             | A4            | Right     | B       ]
    [RFSUB          | M3            | Left      | B       ]
    [LPADCD1NSE2    | C11           | Left      | B       ]
    [DNU            | K8            | Left      | B       ]

  no-connect(self.DNU)
    
  assign-landpattern(bga131_0p35_4p7x4p7-1)
  ; assign-symbols([
  ;   Ref("A") => AMA4B2KK-KXR-B2_1A
  ;   Ref("B") => AMA4B2KK-KXR-B2_1B
  ; ])
  make-box-symbol()

  make-supports()
  make-properties()

; =====================================
; =====================================
; Hardware Design Generator
; For Apollo4 and Apollo3 SoC Families
; A-SOCAPG-UGGA02EN v1.0
; =====================================
; =====================================

; This code is an implementation of detailed design guidelines for the Apollo3 and Apollo4 SoC
; families. This document is to be used in conjunction with the selected SoC documentation,
; including datasheet, programmer’s guide (if applicable), and errata. See Reference Documents
; for a comprehensive list.

public pcb-module module (options:Tuple<KeyValue<Symbol,?>>):
  val settings = Settings(DEFAULT-SETTINGS)
  for entry in options do :
    settings[key(entry)] = value(entry)

  pin VDD5V0
  pin VDD1V8
  pin GND

  port debug : swd()
  port rst : reset

  public inst apollo : ambiq-demo/components/AMBIQ/AMA4B2KK-KXR/component

  val gnd-pins = [apollo.VSSVCO apollo.VSSS apollo.VSSB apollo.VSSP 
                  apollo.VSSA apollo.VSS apollo.VSSAUSB apollo.VSSAUDD 
                  apollo.VSSAUDA apollo.VSS18]
  for p in gnd-pins do :
    net (GND p)

  ; =====================================
  ; =====================================
; Table 2-1: Apollo4 Recommended Internal Supply and Capacitor Values
;  1. Recommend use of 5 V or greater caps for 1.9 V rails.
;  2. Recommend use of 10 V caps for 3.3 V rails.
;  3. Do not float any supply inputs. If a supply is not powered, it should be grounded.
  ; =====================================
  ; =====================================
  val decouple-array = [
    [apollo.VDDC,       GND,          2.2e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDC_LV,    GND,          2.2e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDS,       GND,          2.2e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDF,       GND,          2.2e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDF,       apollo.VDDP,  2.2e-6,   "0201",   10.0, "X5R"] ; ERR087 suggestion
    [apollo.LPADC_VREF, GND,          0.1e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDP,       GND,          1.0e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDH,       GND,          1.0e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDH2,      GND,          1.0e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDA,       GND,          1.0e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDAUDD,    GND,          2.2e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDAUDA,    GND,          2.2e-6,   "0201",   10.0, "X5R"]
    [apollo.VDDB,       GND,          2.2e-6,   "0201",   10.0, "X5R"]
  ]
  add-caps(decouple-array)

  inst REG1V8 : settings[`regulator-1V8]
  inst REG1V8_LN : settings[`regulator-1V8LN]

  net VDD_5V0 (REG1V8.IN VDD5V0 REG1V8_LN.IN)
  net VDD_1V8 (REG1V8.OUT apollo.VDDH2 apollo.VDDAUDD apollo.VDDB apollo.VDDP apollo.VDDH VDD1V8)
  net VDD_1V8_LN (REG1V8_LN.OUT apollo.VDDAUDA apollo.VDDA)
  net (GND REG1V8.GND REG1V8_LN.GND)

  symbol(VDD_5V0)     = ocdb/utils/symbols/altium-power-bar-sym
  symbol(VDD_1V8)     = ocdb/utils/symbols/altium-power-bar-sym
  symbol(VDD_1V8_LN) = ocdb/utils/symbols/altium-power-bar-sym

  ; =====================================
  ; =====================================
  ; 3. SIMO Buck Inductor Selection
  ; =====================================
  ; =====================================
  ;  3.3 Guidelines
  ; Recommended SIMO buck inductor for the Apollo4 and Apollo3 families is
  ; described below.
  ; - Apollo3 and Apollo4: 2.2 μH
  ; - Saturation current > 400 mA (> 500 mA preferred)
  ; - Maximum DC resistance < 0.55 Ω
  ; - Operating frequency range > 20 MHz
  val simo-ind-params = ["inductance" => 2.2e-6, "min-saturation-current" => 0.5, "max-dc-resistance" => 0.55]
  val simo-ind = ind-strap(apollo.SIMOBUCK_SWSEL, apollo.SIMOBUCK_SW, simo-ind-params)

  ; =====================================
  ; =====================================
  ; 4. BLE Buck Induction Selection
  ; =====================================
  ; =====================================
  if settings[`enable-bluetooth] :
    val ble-decouple = [
      [apollo.VDDBH,      GND,          4.7e-6,   "0402",   10.0, "X5R"]
      [apollo.VDDBH_RF,   GND,          1.0e-6,   "0201",   10.0, "X5R"]
    ]
    add-caps(ble-decouple)
    val ble-ind = ind-strap(apollo.VDDBH, apollo.VDDBH_SW, ["inductance" => 1.0e-6, "min-saturation-current" => 0.8, "max-dc-resistance" => 0.55])
    res-strap(apollo.VDDBH, apollo.VDDBH_RF, 0.0)
  else:
    net (GND apollo.VDDBH apollo.VDDBH_SW apollo.VDDBH_RF)

  ; =====================================
  ; =====================================
  ; 5. 32 kHz XTAL Selection
  ; =====================================
  ; =====================================
  ; The recommended 32 kHz XTAL circuit components used for both the Apollo3 and
  ; Apollo4 SoC families are described below.
  ; - 7 pF maximum load capacitance per pin
  ; - 90 kΩ or less ESR
  ; - Start-up time < 300 ms
  inst XTAL-RTC : settings[`xtal-32k]
  require xls : low-freq-oscillator from apollo
  require xlsxtal : low-freq-oscillator from XTAL-RTC
  net (xls xlsxtal)
  schematic-group(XTAL-RTC) = apollo.clock
  ; val rtc-xtal-params = ["frequency" => 32768, "load-capacitance" => 4.0e-12, "ESR" => 90.0e3, "frequency-tolerance" => 20.0e-6 * 32.768e3]

  ; TODO: 7. Add option for external clock source for 32K_EXTCLK circuit to replace crystal

  ; =====================================
  ; =====================================
  ; 6. 32 MHz XTAL Selection
  ; =====================================
  ; =====================================
  ; The Apollo4 32 MHz XTAL circuitry is designed to work with crystals specified for 6
  ; pF of load capacitance, a maximum ESR of 100 Ω, and a maximum of ± 40 PPM
  ; including initial tolerance/aging/temperature drift. It is recommended to choose a
  ; crystal with tighter tolerance to account for board-to-board load capacitance vari-
  ; ation.

  inst XTAL-HS : settings[`xtal-32M]
  require xhs : high-freq-oscillator from apollo
  require xhsxtal : high-freq-oscillator from XTAL-HS
  net (xhs xhsxtal)
  net (GND XTAL-HS.p[2] XTAL-HS.p[4])
  schematic-group(XTAL-HS) = apollo.clock
  ; val hs-xtal-params = ["frequency" => 32.0e6, "load-capacitance" => 6.0e-12, "ESR" => 100, "frequency-tolerance" => 40.0e-6 * 32.0e6]


  ; =====================================
  ; =====================================
  ; 8. Apollo4 USB Guidelines
  ; =====================================
  ; =====================================
  if settings[`enable-usb] :
    port usb : usb-2-data
    net (apollo.USB0PP, usb.P)
    net (apollo.USB0PN, usb.N)

    ; Table 2-2: Apollo4 Recommended External Supply and Capacitor Values
    val usb-decouple = [
      [apollo.VDDUSB33,   GND,          2.2e-6,   "0201",   10.0, "X5R"]
      [apollo.VDDUSB0P9,  GND,          2.2e-6,   "0201",    5.0, "X5R"]
    ]
    add-caps(usb-decouple)

    inst REG3V3 : settings[`regulator-3V3USB]
    inst REG0V9 : settings[`regulator-0V9USB]
    net (VDD5V0 REG3V3.IN)
    net VDD_3V3 (REG0V9.IN REG3V3.OUT)
    net (REG3V3.GND REG0V9.GND GND)

    ; On system power-up, VDDUSB33 and VDDUSB0P9 should be in the OFF state.
    ; For power-sequencing considerations, see the Apollo4 SoC Datasheet.
    inst loadsw : ambiq-demo/components/TEXAS-INSTRUMENTS/TPS22976/module
    require vddusb33en : gpio from apollo
    require vddusb09en : gpio from apollo

    net VDD_0V9 (REG0V9.OUT loadsw.IN2)
    net (VDD_3V3 loadsw.IN1 loadsw.BIAS)
    net (GND loadsw.GND)

    net (loadsw.EN1 vddusb33en.gpio)
    net (loadsw.EN2 vddusb09en.gpio)

    net VDD_USB3V3 (loadsw.OUT2 apollo.VDDUSB0P9)
    net VDD_USB0V9 (loadsw.OUT1 apollo.VDDUSB33)
    symbol(VDD_3V3)     = ocdb/utils/symbols/altium-power-bar-sym
    symbol(VDD_0V9)     = ocdb/utils/symbols/altium-power-bar-sym
    symbol(VDD_USB3V3)  = ocdb/utils/symbols/altium-power-bar-sym
    symbol(VDD_USB0V9)  = ocdb/utils/symbols/altium-power-bar-sym
    schematic-group([REG3V3 REG0V9 loadsw]) = power
  else:
    ; 8.3.1 USB Unused
    ; If the USB peripheral is not used, please leave the USB data pads (USB0PP and
    ; USB0PN) floating, and connect the USB PHY power rails (VDDUSB33 and
    ; VDDUSB0P9) to ground. 
    no-connect(apollo.USB0PN)
    no-connect(apollo.USB0PP)
    property(apollo.VDDUSB0P9.power-pin)  = PowerPin(typ(0.0))
    property(apollo.VDDUSB33.power-pin)   = PowerPin(typ(0.0))
    net (apollo.VDDUSB0P9 GND)
    net (apollo.VDDUSB33 GND)

  ; =====================================
  ; =====================================
  ; 9. Apollo4 MIPI DSI PHY Guidelines
  ; =====================================
  ; =====================================
  if settings[`enable-mipi] :
    port mipi : mipi-d-phy
    require m : mipi-d-phy from apollo
    ; net (m mipi)
    net (m.data.N mipi.data.N)
    net (m.data.P mipi.data.P)
    net (m.clk.N mipi.clk.N)
    net (m.clk.P mipi.clk.P)
    ; topology-segment(m mipi)
    topology-segment(m.data.N mipi.data.N)
    topology-segment(m.data.P mipi.data.P)
    topology-segment(m.clk.N mipi.clk.N)
    topology-segment(m.clk.P mipi.clk.P)
    val mipi-decouple = [
      [apollo.VDD18,      GND,          2.2e-6,   "0201",    5.0, "X5R"]
    ]
    add-caps(mipi-decouple)
    inst vdd18sw : ambiq-demo/components/TEXAS-INSTRUMENTS/TPS22919/module
    require vdd18en : gpio from apollo
    net (vdd18en.gpio vdd18sw.ON)
    net (REG1V8.OUT vdd18sw.IN)
    net (vdd18sw.OUT apollo.VDD18)
  else :
    property(apollo.VDD18.power-pin)   = PowerPin(typ(0.0))
    net (GND apollo.VDD18)
    no-connect(apollo.MIPI_D0P)    
    no-connect(apollo.MIPI_D0N)    
    no-connect(apollo.MIPI_CLKP)    
    no-connect(apollo.MIPI_CLKN)


  if settings[`enable-debug] :
    ; =====================================
    ; =====================================
    ; Apollo4 DEBUG
    ; =====================================
    ; =====================================
    require apollo-swd:swd() from apollo

  ; Check SWDCK and SWDIO, which should be connected to a debug
  ; header.
  ; - SWDCK needs a 10 kΩ pull-down resistor
  ; - SWDIO needs a 10 kΩ pull-up resistor
    res-strap(apollo-swd.swdclk apollo.VDDH, 10.0e3)
    res-strap(apollo-swd.swdio GND, 10.0e3)

    inst debug-conn : settings[`debug-connector]
    require debug-swd:swd() from debug-conn
    net (apollo-swd debug-swd)

; Check the reset pin. External 1nF capacitor recommended to filter exter-
; nal noise/glitches. Consider including pads for an external pull-up resis-
; tor. If an external IC is connected to control nRST, it must be open-drain.
  require areset:reset from apollo
  cap-strap(areset.reset, GND, 1.0e-9)

  check ambiq-apollo4(XTAL-RTC, XTAL-HS, simo-ind)